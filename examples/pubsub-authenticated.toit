// Copyright (C) 2022 Toitware ApS.
// Use of this source code is governed by a Zero-Clause BSD license that can
// be found in the EXAMPLES_LICENSE file.

import mqtt
import net
import encoding.json
import tls
import net.x509

/**
An example demonstrating how to subscribe and publish messages on
  a broker, connected through TLS.

This examples also demonstrates how to use MQTT username/password.

Be default uses test.mosquitto.org. This server is publicly accessible and
  shared among many users.
*/


HOST ::= "test.mosquitto.org"
PORT ::= 8883

// Alternatively, you could install the certificate_roots package
// and use 'ISRG_ROOT_X1':
//   `SERVER_CERTIFICATE ::= certificate_roots.ISRG_ROOT_X1`
SERVER_CERTIFICATE ::= MOSQUITTO_ORG_CERTIFICATE

CLIENT_ID ::= "toit-pubsub-$(random)"
USERNAME ::= "rw"
PASSWORD ::= "readwrite"

TOPIC_PREFIX ::= "toit/pubsub-$(random)"

main:
  transport := mqtt.TcpTransport.tls net.open --host=HOST
    --root_certificates=[SERVER_CERTIFICATE]

  client := mqtt.Client --transport=transport

  options := mqtt.SessionOptions
      --clean_session
      --client_id = CLIENT_ID
      --username = USERNAME
      --password = PASSWORD
  client.start --options=options
      --on_error=:: print "Client error: $it"

  print "connected to broker"

  client.subscribe "$TOPIC_PREFIX/+":: | topic payload |
    if payload == #[0]:
      // Received the token to stop listening.
      client.close
    else:
      print "$topic: $payload.to_string_non_throwing"

  10.repeat:
    payload := json.encode {
      "key": it
    }
    topic := ?
    if it % 2 == 0: topic = "$TOPIC_PREFIX/foo"
    else: topic = "$TOPIC_PREFIX/bar"
    client.publish topic payload
    sleep --ms=1000

  // Send a signal to stop listening for messages.
  client.publish "$TOPIC_PREFIX/done" #[0]

  print "done"

// The certificate given by https://test.mosquitto.org/.
MOSQUITTO_ORG_CERTIFICATE ::= x509.Certificate.parse """
-----BEGIN CERTIFICATE-----
MIIEAzCCAuugAwIBAgIUBY1hlCGvdj4NhBXkZ/uLUZNILAwwDQYJKoZIhvcNAQEL
BQAwgZAxCzAJBgNVBAYTAkdCMRcwFQYDVQQIDA5Vbml0ZWQgS2luZ2RvbTEOMAwG
A1UEBwwFRGVyYnkxEjAQBgNVBAoMCU1vc3F1aXR0bzELMAkGA1UECwwCQ0ExFjAU
BgNVBAMMDW1vc3F1aXR0by5vcmcxHzAdBgkqhkiG9w0BCQEWEHJvZ2VyQGF0Y2hv
by5vcmcwHhcNMjAwNjA5MTEwNjM5WhcNMzAwNjA3MTEwNjM5WjCBkDELMAkGA1UE
BhMCR0IxFzAVBgNVBAgMDlVuaXRlZCBLaW5nZG9tMQ4wDAYDVQQHDAVEZXJieTES
MBAGA1UECgwJTW9zcXVpdHRvMQswCQYDVQQLDAJDQTEWMBQGA1UEAwwNbW9zcXVp
dHRvLm9yZzEfMB0GCSqGSIb3DQEJARYQcm9nZXJAYXRjaG9vLm9yZzCCASIwDQYJ
KoZIhvcNAQEBBQADggEPADCCAQoCggEBAME0HKmIzfTOwkKLT3THHe+ObdizamPg
UZmD64Tf3zJdNeYGYn4CEXbyP6fy3tWc8S2boW6dzrH8SdFf9uo320GJA9B7U1FW
Te3xda/Lm3JFfaHjkWw7jBwcauQZjpGINHapHRlpiCZsquAthOgxW9SgDgYlGzEA
s06pkEFiMw+qDfLo/sxFKB6vQlFekMeCymjLCbNwPJyqyhFmPWwio/PDMruBTzPH
3cioBnrJWKXc3OjXdLGFJOfj7pP0j/dr2LH72eSvv3PQQFl90CZPFhrCUcRHSSxo
E6yjGOdnz7f6PveLIB574kQORwt8ePn0yidrTC1ictikED3nHYhMUOUCAwEAAaNT
MFEwHQYDVR0OBBYEFPVV6xBUFPiGKDyo5V3+Hbh4N9YSMB8GA1UdIwQYMBaAFPVV
6xBUFPiGKDyo5V3+Hbh4N9YSMA8GA1UdEwEB/wQFMAMBAf8wDQYJKoZIhvcNAQEL
BQADggEBAGa9kS21N70ThM6/Hj9D7mbVxKLBjVWe2TPsGfbl3rEDfZ+OKRZ2j6AC
6r7jb4TZO3dzF2p6dgbrlU71Y/4K0TdzIjRj3cQ3KSm41JvUQ0hZ/c04iGDg/xWf
+pp58nfPAYwuerruPNWmlStWAXf0UTqRtg4hQDWBuUFDJTuWuuBvEXudz74eh/wK
sMwfu1HFvjy5Z0iMDU8PUDepjVolOCue9ashlS4EB5IECdSR2TItnAIiIwimx839
LdUdRudafMu5T5Xma182OC0/u/xRlEm+tvKGGmfFcN0piqVl8OrSPBgIlb+1IKJE
m/XriWr/Cq4h/JfB7NTsezVslgkBaoU=
-----END CERTIFICATE-----
"""
